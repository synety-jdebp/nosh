#compdef init telinit svcadm systemctl initctl rcctl system-control system-manager per-user-manager service-manager svstat service-status svc service-control start stop reset preset console-terminal-emulator console-convert-kbdmap console-clear resizecons console-resize console-multiplexor console-fb-realizer console-ncurses-realizer svscan service-dt-scanner svok service-is-ok svup service-is-up svshow service-show emergency rescue normal reboot halt haltsys poweroff shutdown clearenv setenv unsetenv
## **************************************************************************
## For copyright and licensing terms, see the file named COPYING.
## **************************************************************************
#
# Z Shell command-line completion

function _system_control_subcommands()
{
	local -a commands
	commands=(
	'init:run initial action'
	'emergency:start emergency mode'
	'rescue:start rescue mode'
	'normal:start normal mode'
	'halt:halt the system'
	'poweroff:power off the system'
	'reboot:reboot the system'
	'start:start a service'
	'activate:start a service'
	'isolate:start a service'
	'stop:stop a service'
	'deactivate:stop a service'
	'reset:reset a service to its preset state'
	'enable:configure a service to auto-start'
	'disable:configure a service to not auto-start'
	'preset:configure a service auto-start according to config files'
	'status:display service status'
	'show:print machine-readable service status'
	'show-json:print machine-readable service status'
	"find:locate a service's service directory"
	'is-active:query whether a service is started'
	'is-loaded:query whether a service is loaded'
	'is-enabled:query whether a service is enabled'
	'is-service-manager-client:query whether the service manager is running'
	'hangup:send a HUP signal to a running service'
	'condrestart:send a TERM signal to a running service'
	'try-restart:send a TERM signal to a running service'
	'force-reload:send a TERM signal to a running service'
	'convert-systemd-units:convert systemd unit files to a service bunbdle'
	'convert-fstab-services:convert fstab to service bundles'
	'nagios-check-service:nagios plug-in for checking services'
	'load-kernel-module:load a kernel module'
	'unload-kernel-module:unload a kernel module'
	"cat:print a service bundle's control scripts"
	'set-service-env:change a standard-format env/ directory for a service'
	'print-service-env:display a standard-format env/ directory for a service'
	'escape:escape names as the conversion utilities do'
	)
	_describe -t command 'sub-commands' commands
}

function _system_control_subarguments()
{
	local lstate state tmpargv tmp cache cur next
	local -a common dir
	local w1="$words[1]"

	common=(-s '--help[Display option help]' '--usage[Display option usage]')
	dir=(/var/local/sv /var/sv /etc/service-bundles/services /etc/service-bundles/targets)
	words[1]="$words[1] $words[2]"

	local curcontext="${curcontext%:*:*}:system-control-$words[2]:" 

	case "$words[2]" in
	emergency|rescue|normal|halt|poweroff|reboot)
		_arguments -S $common -- ;;
	init)
		_arguments -S $common '2:run level:_runlevels' -- ;;
	load-kernel-module|unload-kernel-module)
		_arguments -S $common '2:module name:_files' -- ;;
	set-service-env)
		_arguments -A '-*' $common '2:service name:_path_files -/ -W dir' '3:name:' '4::value:' -- ;;
	print-service-env)
		_arguments -A '-*' $common '2:service name:_path_files -/ -W dir' '3::value:' -- ;;
	convert-systemd-units)
		_arguments -A '-*' $common '2:unit name:_files' -- ;;
	escape)
		_arguments -A '-*' $common '*:name: ' -- ;;
	*)
		_arguments -A '-*' $common '*:service name:_path_files -/ -W dir' -- ;;
	esac

	words[1]="${w1}"
}

function _runlevels()
{
	local -a runlevels
	runlevels=(
	'0:poweroff'
	'1:rescue'
	'2:normal'
	'3:normal'
	'4:normal'
	'5:normal'
	'6:reboot'
	'm:normal'
	'auto:normal'
	'H:halt'
	'h:halt'
	'S:rescue'
	's:rescue'
	'single:rescue'
	'B:emergency'
	'b:emergency'
	'emergency:emergency'
	)
	_describe -t runlevel 'run levels' runlevels
}

local lstate state
local -a common
common=(-s '--help[Display option help]' '--usage[Display option usage]')

case "${service}" in
service-manager|per-user-manager|system-manager)
	_message -r "${service} has no options or parameters." ;;
systemctl|initctl|rcctl|svcadm|system-control) 
	_arguments -A '-*' $common '1:subcommand:_system_control_subcommands' '*:subargs:_system_control_subarguments' -- ;;
init|telinit)
	_arguments -S $common '1:runlevel:_runlevels' -- ;;
svshow|service-show|svstat|service-status|svok|service-is-ok|svup|service-is-up|svc|service-control)
	_arguments -A '-*' '*:service directory:_directories' -- ;;
svscan|service-dt-scanner)
	_arguments -A '-*' $common '1:scan directory:_directories' -- ;;
console-multiplexor)
	_arguments -A '-*' $common '1:composite virtual console directory:_directories' '*:source virtual console directory:_directories' -- ;;
console-terminal-emulator)
	_arguments -A '-*' $common '1:virtual console directory:_directories' -- ;;
console-fb-realizer)
	_arguments -A '-*' $common '1:virtual console directory:_directories' '2:framebuffer device:_files' -- ;;
console-ncurses-realizer)
	_arguments -A '-*' $common '1:virtual console directory:_directories' -- ;;
console-convert-kbdmap)
	_arguments -A '-*' $common '*:BSD kbd map files:_directories' -- ;;
shutdown)
	_arguments -A '-*' $common '1:time:' '2:message:' -- ;;
setenv)
	_arguments -A '-*' $common '1:variable:' '2:value:' -- ;;
unsetenv)
	_arguments -A '-*' $common '1:variable:' -- ;;
*)
	_arguments -A '-*' $common -- ;;
esac
